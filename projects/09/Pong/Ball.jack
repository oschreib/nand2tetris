/**
 * Implements a ball graphic. The ball moves around the field until it hits
 * a border, or is bumped by a user call.
 */
class Ball {

    // Location on the screen
    field int x, y;

    // The size of the ball
    field int size;

    // The direction across x
    field int dx;

    // The direction across y
    field int dy;

    /** Constructs a new ball with a given location and size. */
    constructor Ball new(int Ax, int Ay, int Asize) {
        let x = Ax;
        let y = Ay;
        let size = Asize;
		let dx = 1;
		if(((Ax / 2) * 2) = Ax) {
			// Add randomness to the starting  movement
			let dx = -1;
		}
        let dy = -1;
		if(((Ax / 4) * 4) = Ax) {
			// Add randomness to the starting  movement
			let dy = 1;
		}

        do draw();

        return this;
    }
	
	/** Getters */
	method int x() { return x; }
	method int y() { return y; }
	method int size() { return size; }

    /** Deallocates the object's memory. */
    method void dispose() {
		do erase();
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the square on the screen. */
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

    /** Erases the square from the screen. */
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + size, y + size);
        return;
    }

	method void bump() {
		let dy = dy * -1;
		return;
	}
    method void move() {
        if (((x + dx) > (507 - size)) | (x + dx < size)) {
            let dx = dx * -1;
        }

        if (((y + dy) < size) | ((y + dy) > (253 - size))) {
            let dy = dy * -1;
        }
		
		if(dx > 0) {
			do moveRight(2);
		}
		if(dx < 0) {
			do moveLeft(2);
		}
        if(dy > 0) {
			do moveDown(2);
		}
		if(dy < 0) {
			do moveUp(2);
		}
        return;
    }
	
	/** Moves up by step pixels. */
    method void moveUp(int step) {
        if (y > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
            let y = y - step;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + size, y + 1);
        }
        return;
    }

    /** Moves down by step pixels. */
    method void moveDown(int step) {
        if ((y + size) < 254) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + size, y + 1);
            let y = y + step;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
        }
        return;
    }

    /** Moves left by step pixels. */
    method void moveLeft(int step) {
        if (x - step > 1) {
            do Screen.setColor(false);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
            let x = x - step;
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 1, y + size);
        }
        return;
    }

    /** Moves right by step pixels. */
    method void moveRight(int step) {
        if ((x + size + step) < 510) {
            do Screen.setColor(false);
            do Screen.drawRectangle(x, y, x + 1, y + size);
            let x = x + step;
            do Screen.setColor(true);
            do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
        }
        return;
    }
}
